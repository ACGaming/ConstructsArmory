buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.wynprice.cursemaven:CurseMaven:1.2.3'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.wynprice.cursemaven'

version = "1.12.2-1.2.5.4"
group = "c4.conarm"
archivesBaseName = "conarm"

repositories {
    maven {
        name = "CraftTweaker/ContentTweaker"
        url = "http://maven.blamejared.com"
    }
    maven {
        name = "Tinkers/JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2772"
    runDir = "run"
    useDepAts = true
    mappings = "stable_39"

    replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

dependencies {
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.49"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.13.0.171"
    compile curse.resolveUrl("https://www.curseforge.com/minecraft/mc-mods/tinkers-tool-leveling/files/2630860")

    deobfProvided "CraftTweaker2:CraftTweaker2-API:4.1.13.508"
    deobfProvided "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.13.508"
    deobfProvided "com.teamacronymcoders:ContentTweaker:1.12.2-4.9.1"
    deobfProvided "com.teamacronymcoders.base:base:1.12.2-3.11.0"

    deobfProvided "mezz.jei:jei_1.12.2:4.13.1.225:api"
    runtime "mezz.jei:jei_1.12.2:4.13.1.225"
}

sourceSets {
    main {
        resources {
            srcDirs += 'docs'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {

    onlyIf {
        project.hasProperty('keyStore')
    }

    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar